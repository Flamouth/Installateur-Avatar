; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Avatar"
#define MyAppVersion "2.2"
#define MyAppPublisher "Avatar Home Automation"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1A7A6E82-ECEE-41A7-B430-2BE07BF868FE}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DisableWelcomePage=no
AppPublisher={#MyAppPublisher}
DefaultDirName=C:\{#MyAppName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputDir=C:\Users\Avatar\Desktop
OutputBaseFilename=setup
SetupIconFile=C:\Users\Avatar\Desktop\Avatar\hal_421_1.ico
Compression=lzma
SolidCompression=yes
WizardImageFile=C:\Users\Avatar\Desktop\Avatar\img1.bmp
WizardSmallImageFile=C:\Users\Avatar\Desktop\Avatar\img2.bmp
PrivilegesRequired=lowest

#include <idp.iss>

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Types]
Name: full;    Description: "Client et Serveur"
Name: custom;  Description: "Client"; Flags: iscustom
Name: compact;    Description: "Serveur"


[Components]
Name: app; Description: "Client Avatar";  Types: full custom; Flags: fixed
Name: app2;  Description: "Serveur Avatar";  Types: full compact; Flags: fixed

[Icons]

Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\Installation\Avatar.cmd";

[Files]
Source: "{tmp}\Avatar-Windows-Client.zip";  DestDir: "{app}\Installation"; Flags: external; ExternalSize: 93150000; Components: app
Source: "{tmp}\Avatar-Serveur.zip";  DestDir: "{app}\Installation"; Flags: external; ExternalSize: 75300000; Components: app2
Source: "C:\Users\Avatar\Desktop\Avatar\Installation\7za.exe"; DestDir: "{app}\Installation";
Source: "C:\Users\Avatar\Desktop\Avatar\Installation\7za.dll"; DestDir: "{app}\Installation"; Flags: ignoreversion
Source: "C:\Users\Avatar\Desktop\Avatar\Installation\7zxa.dll"; DestDir: "{app}\Installation"; Flags: ignoreversion
Source: "C:\Users\Avatar\Desktop\Avatar\Avatar.zip"; DestDir: "{app}\Installation"; 

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]


procedure InitializeWizard;
begin
    idpAddFileComp('https://raw.githubusercontent.com/Spikharpax/Avatar-Client/master/Avatar-Windows-Client.zip',  ExpandConstant('{tmp}\Avatar-Windows-Client.zip'),  'app');
    idpAddFileComp('https://raw.githubusercontent.com/Spikharpax/Avatar-Serveur/master/Avatar-Serveur.zip',  ExpandConstant('{tmp}\Avatar-Serveur.zip'),  'app2');
    idpDownloadAfter(wpReady);
 
end;

procedure SetProgressMax(Ratio: Integer);
begin
  WizardForm.ProgressGauge.Max := WizardForm.ProgressGauge.Max * Ratio;
end;

procedure UpdateProgress(Position: Integer);
begin
  WizardForm.ProgressGauge.Position := Position * WizardForm.ProgressGauge.Max div 100;
end;


[Run]
Filename: "{app}\Installation\7za.exe"; Parameters: "x -y -o""{app}\Client"" ""{app}\Installation\Avatar-Windows-Client.zip"""; StatusMsg: "Merci de patientez, extraction du Client en cours... . Cela peut prendre du temps"; BeforeInstall: UpdateProgress(35); AfterInstall: UpdateProgress(50); Flags: runhidden
Filename: "{app}\Installation\7za.exe"; Parameters: "x -y -o""{app}\Serveur"" ""{app}\Installation\Avatar-Serveur.zip"""; StatusMsg: "Merci de patientez, extraction du Serveur en cours... . Cela peut prendre du temps"; BeforeInstall: UpdateProgress(60); AfterInstall: UpdateProgress(78); Flags: runhidden
Filename: "{app}\Installation\7za.exe"; Parameters: "x -y -o""{app}"" ""{app}\Installation\Avatar.zip"""; StatusMsg: "Merci de patientez, extraction des fichiers en cours..."; BeforeInstall: UpdateProgress(90); AfterInstall: UpdateProgress(100);
Filename: "{app}\Installation\Aide_Avatar.exe"; Description: {cm:LaunchProgram, Aide_Avatar.exe}; Flags: nowait postinstall skipifsilent